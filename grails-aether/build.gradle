ext {
    aetherVersion = "1.0.2.v20150114"
    mavenVersion = "3.2.5"
}

dependencies {
    compile("org.eclipse.aether:aether-api:${aetherVersion}")
    compile("org.eclipse.aether:aether-util:${aetherVersion}")
    compile("org.eclipse.aether:aether-impl:${aetherVersion}")
    compile("org.eclipse.aether:aether-connector-basic:${aetherVersion}")
    compile("org.eclipse.aether:aether-transport-http:${aetherVersion}")
    compile("org.eclipse.aether:aether-transport-file:${aetherVersion}")
    compile("org.apache.maven:maven-aether-provider:$mavenVersion")
    compile "org.apache.maven:maven-settings-builder:$mavenVersion"
    compile 'org.eclipse.sisu:org.eclipse.sisu.plexus:0.0.0.M5'

    compile project(":grails-bootstrap")

    runtime "org.slf4j:slf4j-simple:$slf4jVersion"
}

install.doLast {
    final aetherConf = new File(rootProject.projectDir, "conf/aether-starter.conf")
    aetherConf.parentFile.mkdirs()

    aetherConf.withPrintWriter { writer ->
        writer.println "load \${grails.home}/dist/grails-aether-${grailsVersion}.jar"
        def appendDependencies = {
            def fileName = it.file.name
            if(! ['jline', 'jansi','jna','hibernate', 'asm', 'cglib', 'groovy-all', 'ant', 'servlet-api'].any { fileName.contains(it) }) {

                def mid = it.moduleVersion.id

                writer.println "load \${grails.home}/lib/${mid.group}/${mid.name}/jars/$fileName"
            }
        }
        configurations.runtime.resolvedConfiguration.resolvedArtifacts.each appendDependencies
    }
}
